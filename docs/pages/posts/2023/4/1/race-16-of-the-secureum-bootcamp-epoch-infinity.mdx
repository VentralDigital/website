# RACE #16 Of The Secureum Bootcamp Epoch∞

import { Callout } from 'nextra/components'

This is a Write-Up of RACE-16, Quiz of the [Secureum Bootcamp](https://twitter.com/0xRajeev/status/1470910752085065731) for Ethereum Smart Contract Auditors.   It was designed by recently joined Secureum Mentor [Jon Stephens](https://twitter.com/FormallyJon) and his company [Veridise](https://twitter.com/VeridiseInc).

<Callout type="info">
  Participants of this quiz had to **answer 8 questions within the strict time limit of 16 minutes**. If you’re reading this in preparation for participating yourself, it’s best to give it a try under the same time limit!

  As usual, I waited for submissions to close before publishing it and, to stay true to the original, I omitted syntax highlighting. Feel free to copy it into your favorite editor, but do so after starting the timer!
</Callout>

<p className="text-xs text-right">March 25, 2023 by [patrickd](/about#patrickd)</p>

<style jsx>{`
  input[type=checkbox] {
    width: 15px;
    height: 15px;
  }
  li {
    padding: 0.5em;
  }
`}</style>

export function HiddenSolution({ children }) {
  return (
    <details closed className="last-of-type:mb-0 rounded-lg bg-neutral-50 dark:bg-neutral-800 p-2 mt-4">
      <summary>
        <strong className="text-lg">Solution</strong>
      </summary>
      <div className="nx-p-2">{children}</div>
    </details>
  )
}

---

### Code

<Callout type="info">
All 8 questions in this RACE are based on the below contract. This is the same contract you will see for all the 8 questions in this RACE. The question is below the shown contract.
</Callout>


```plaintext showLineNumbers
// SPDX-License-Identifier: Unlicense
pragma solidity ^0.8.0;


import "@openzeppelin/contracts/interfaces/IERC3156FlashLender.sol";
import "@openzeppelin/contracts/interfaces/IERC20.sol";


contract FlashLoan is IERC3156FlashLender {
   bytes32 public constant CALLBACK_SUCCESS = keccak256("ERC3156FlashBorrower.onFlashLoan");
   uint256 public fee;


   /**
    * @param fee_ the fee that should be paid on a flashloan
    */
   constructor (
       uint256 fee_
   ) {
       fee = fee_;
   }


   /**
    * @dev The amount of currency available to be lent.
    * @param token The loan currency.
    * @return The amount of `token` that can be borrowed.
    */
   function maxFlashLoan(
       address token
   ) public view override returns (uint256) {
       return IERC20(token).balanceOf(address(this));
   }


   /**
    * @dev The fee to be charged for a given loan.
    * @param token The loan currency. Must match the address of this contract.
    * @param amount The amount of tokens lent.
    * @return The amount of `token` to be charged for the loan, on top of the returned principal.
    */
   function flashFee(
       address token,
       uint256 amount
   ) external view override returns (uint256) {
       return fee;
   }


   /**
    * @dev Loan `amount` tokens to `receiver`, and takes it back plus a `flashFee` after the ERC3156 callback.
    * @param receiver The contract receiving the tokens, needs to implement the `onFlashLoan(address user, uint256 amount, uint256 fee, bytes calldata)` interface.
    * @param token The loan currency. Must match the address of this contract.
    * @param amount The amount of tokens lent.
    * @param data A data parameter to be passed on to the `receiver` for any custom use.
    */
   function flashLoan(
       IERC3156FlashBorrower receiver,
       address token,
       uint256 amount,
       bytes calldata data
   ) external override returns (bool){
       uint256 oldAllowance = IERC20(token).allowance(address(this), address(receiver));
       uint256 oldBal = IERC20(token).balanceOf(address(this));
       require(amount <= oldBal, "Too many funds requested");
       IERC20(token).approve(address(receiver), oldAllowance + amount);

       require(
           receiver.onFlashLoan(msg.sender, token, amount, fee, data) == CALLBACK_SUCCESS,
           "Callback failed"
       );

       uint256 newBal = IERC20(token).balanceOf(address(this));
       if(newBal < oldBal + fee) {
           uint retAmt = oldBal + fee - newBal;
           require(IERC20(token).transferFrom(msg.sender, address(this), retAmt), "All funds not returned");
       }

       if (IERC20(token).allowance(address(this), address(receiver)) > oldAllowance) {
           IERC20(token).approve(address(receiver), oldAllowance);
       }

       return true;
   }
}
```

### Question 1 of 8

Which of the following is an explanation of why `flashLoan()` could revert?

<ul>
  <li><input type="checkbox" /> A. The transaction reverts because a user requested to borrow more than `maxFlashLoan()`</li>
  <li><input type="checkbox" /> B. The transaction reverts because the receiver’s `onFlashLoan()` did not return `CALLBACK_SUCCESS`</li>
  <li><input type="checkbox" /> C. The transaction reverts because the user returned more than `retAmt` funds</li>
  <li><input type="checkbox" /> D. The transaction reverts because a user tried to spend more funds than their allowance in `onFlashLoan()`</li>
</ul>

<HiddenSolution>
**Correct is A, B, D.**

* A. Is implicitly checked in the `require` with message `"Too many funds requested"`
* B. Is explicitly checked in the `require` with message `"Callback failed"`
* C. The user may return more but not less than `retAmt`
* D. An ERC20 `transferFrom()` would revert in that case
</HiddenSolution>

---

### Question 2 of 8

If the `FlashLoan` contract were safe, which of the following invariants should hold at the end of any given transaction for some ERC20 token t?

Note: `old(expr)` evaluates `expr` at the beginning of the transaction.

<ul>
  <li><input type="checkbox" /> A. `t.balanceOf(address(this)) >= old(t.balanceOf(address(this)))`</li>
  <li><input type="checkbox" /> B. `t.balanceOf(address(this)) == old(t.balanceOf(address(this)))` </li>
  <li><input type="checkbox" /> C. `t.balanceOf(address(this)) > old(t.balanceOf(address(this)))` </li>
  <li><input type="checkbox" /> D. `t.balanceOf(address(this)) == old(t.balanceOf(address(this))) + fee` </li>
</ul>

<HiddenSolution>
**Correct is A.**

For the flashloan to be safe, the contract's token balance must be maintained no matter which function is called. It must be (A) because flashloan will cause the token balance to either increase or stay the same (depending on fee) and all other functions should maintain token balances
</HiddenSolution>

---

### Question 3 of 8

Which of the following tokens would be unsafe for the above contract to loan as doing so could result in theft?

<ul>
  <li><input type="checkbox" /> A. ERC223</li>
  <li><input type="checkbox" /> B. ERC677</li>
  <li><input type="checkbox" /> C. ERC777</li>
  <li><input type="checkbox" /> D. ERC1155</li>
</ul>

<HiddenSolution>
**Correct is C.**

This can be attacked by ERC20 contracts with sender-callbacks in `transferFrom()`. ERC777 and ERC1155 are the only ones with callbacks. But ERC1155 will revert as its APIs doesn't match the ones in the flashloan contract, even even then, it would only have receiver-callbacks.
</HiddenSolution>

---

### Question 4 of 8

Which external call made by `flashLoan()` could result in theft if the token(s) identified in the previous question were to be used?

<ul>
  <li><input type="checkbox" /> A. `onFlashLoan()`</li>
  <li><input type="checkbox" /> B. `balanceOf()`</li>
  <li><input type="checkbox" /> C. `transferFrom()`</li>
  <li><input type="checkbox" /> D. `approve()`</li>
</ul>

<HiddenSolution>
**Correct is C.**

ERC777 tokens have potentially dangerous callbacks on `transfer()`/`transferFrom()` that can result in theft.
</HiddenSolution>

---

### Question 5 of 8

What is the purpose of the fee in the `FlashLoan` contract as is?

<ul>
  <li><input type="checkbox" /> A. To increase the size of available flashloans over time</li>
  <li><input type="checkbox" /> B. To pay the owner of the flashloan contract</li>
  <li><input type="checkbox" /> C. To pay those who staked their funds to be flashloaned</li>
  <li><input type="checkbox" /> D. It has no purpose</li>
</ul>

<HiddenSolution>
**Correct is A.**

In the current `FlashLoan` contract, as it is, the sole purpose of the fee is to increase the available funds to loan.
</HiddenSolution>

---

### Question 6 of 8

Which of the following describes the behavior of `maxFlashLoan` for a standard ERC20 token over time?

<ul>
  <li><input type="checkbox" /> A. strictly-increasing</li>
  <li><input type="checkbox" /> B. non-decreasing</li>
  <li><input type="checkbox" /> C. constant</li>
  <li><input type="checkbox" /> D. None of the above</li>
</ul>

<HiddenSolution>
**Correct is B.**

Can't be A since fee may be 0. It could still increase from people injecting tokens, but it may never decrease.
</HiddenSolution>

---

### Question 7 of 8

For some arbitrary ERC20 token `t`, which of the following accurately describes the `FlashLoan` contract’s balance of `t` after a successful (i.e. non-reverting) call to `flashLoan()` (where `t` is the token requested for the flashloan):

<ul>
  <li><input type="checkbox" /> A. The `FlashLoan` contract's balance of token `t` will INCREASE OR STAY THE SAME.</li>
  <li><input type="checkbox" /> B. The `FlashLoan` contract's balance of token `t` will DECREASE OR STAY THE SAME.</li>
  <li><input type="checkbox" /> C. The `FlashLoan` contract's balance of token `t` will STAY THE SAME.</li>
  <li><input type="checkbox" /> D. None of the above.</li>
</ul>

<HiddenSolution>
**Correct is D.**

`flashLoan()` can hypothetically finish successfully with any token that implements the ERC20 interface, even if it is a bogus implementation. Therefore, there are no guarantees on the output of `IERC20(token).balanceOf(user)`.
</HiddenSolution>

---

### Question 8 of 8

Which of the following are guaranteed to hold after a successful (i.e., non-reverting) execution of `flashLoan()`, assuming the token for which the flashloan is requested uses OpenZeppelin’s Standard ERC20 implementation?

<ul>
  <li><input type="checkbox" /> A. The receiver’s balance of “token” increases</li>
  <li><input type="checkbox" /> B. The funds that the `FlashLoan` contract has approved the receiver to spend has either stayed the same or decreased.</li>
  <li><input type="checkbox" /> C. The sum of all flashloans granted by the `FlashLoan` contract is less than the `maxFlashLoan` amount.</li>
  <li><input type="checkbox" /> D. The token balance of any contract/user other than the `FlashLoan` contract, the caller of the `flashLoan()`, and the “receiver” contract will remain the same as before the call to `flashLoan()`.</li>
</ul>

<HiddenSolution>
**Correct is B.**

* A. The token balance of the receiver could increase, decrease, or remain the same in the call to `onFlashLoan()`
* B. This is ensured by the last `if`-statement
* C. `maxFlashLoan` has no relationship to the sum of all flashloans
* D. Other user token balances could be adjusted in the call to `onFlashLoan()`
</HiddenSolution>

---