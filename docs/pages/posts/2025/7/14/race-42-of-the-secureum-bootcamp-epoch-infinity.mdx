# RACE #42 Of The Secureum Bootcamp Epoch∞

import { Callout } from 'nextra/components'
import Head from 'next/head'
import banner from '../../../../secureum-banner.jpg'

<Head>
  <meta property="og:image" content={'https://ventral.digital' + banner.src} />
  <meta name="twitter:image" content={'https://ventral.digital' + banner.src} />
</Head>

This is a write-up for RACE-42, Quiz of the [Secureum Bootcamp](https://twitter.com/0xRajeev/status/1470910752085065731) for Ethereum Smart Contract Auditors. It was designed by [Kaden](https://x.com/0xkaden), an EVM Security Researcher currently specializing in assembly and automated market makers at [Spearbit](https://x.com/spearbit) and [Cantina](https://x.com/cantinaxyz).

<Callout type="info">
  Participants of this quiz had a single attempt to **answer 8 questions within the strict time limit of 16 minutes**. If you’re reading this in preparation for participating yourself, it’s best to give it a try under the same time limit!
</Callout>

<p className="text-xs text-right">July 14, 2025 by [patrickd](/about/#patrickd)</p>

<style jsx>{`
  input[type=checkbox] {
    width: 15px;
    height: 15px;
  }
  li {
    padding: 0.5em;
  }
`}</style>

export function HiddenSolution({ children }) {
  return (
    <details className="last-of-type:mb-0 rounded-lg bg-neutral-50 dark:bg-neutral-800 p-2 mt-4">
      <summary>
        <strong className="text-lg">Solution</strong>
      </summary>
      <div className="nx-p-2">{children}</div>
    </details>
  )
}

---

### Question 1 of 8

```plaintext showLineNumbers copy filename="MemSafe.sol"
pragma solidity ^0.8.0;

contract MemSafe {
    function memSafe0(bytes4 selector, uint256 a, uint256 b, uint256 c) external pure {
        assembly ("memory-safe") {
            let fmp := mload(0x40)

            mstore(0x00, selector)
            mstore(0x04, a)
            mstore(0x24, b)
            mstore(0x44, c)

            let hash := keccak256(0x00, 0x64)

            mstore(0x40, fmp)
        }
    }

    function memSafe1(bytes4 selector, uint256 a, uint256 b) external pure {
        assembly ("memory-safe") {
            mstore(0x00, selector)
            mstore(0x04, a)
            mstore(0x24, b)

            let hash := keccak256(0x00, 0x44)

            mstore(0x24, 0x00)
        }
    }

    function memSafe2(uint256 a, uint256 b) external pure {
        assembly ("memory-safe") {
            mstore(0x00, a)
            mstore(0x20, b)

            let hash := keccak256(0x00, 0x40)
        }
    }

    function memSafe3(uint256 a, uint256 b, uint256 c, uint256 d) external pure {
        assembly ("memory-safe") {
            mstore(0x00, a)
            mstore(0x20, b)
            mstore(0x40, c)
            mstore(0x60, d)

            let hash := keccak256(0x00, 0x80)

            mstore(0x00, hash)
            revert(0x00, 0x20)
        }
    }
}
```

Which of the above assembly blocks are memory safe?

<ul>
  <li><input type="checkbox" /> A. `memSafe0` </li>
  <li><input type="checkbox" /> B. `memSafe1` </li>
  <li><input type="checkbox" /> C. `memSafe2` </li>
  <li><input type="checkbox" /> D. `memSafe3` </li>
</ul>

<HiddenSolution>
**Correct is B, C, D.**

You're probably already aware of two things: Solidity makes use of the Memory that the EVM offers, and the Solidity compiler has an integrated optimizer that may make use of said memory. The problem is that inline assembly allows using memory in an incompatible way. Therefore, by default, in the presence of any inline assembly block that contains a memory operation or assigns to Solidity variables in memory, the compiler's memory optimizations are globally disabled.

Annotating inline assembly blocks as "memory-safe" indicates that Solidity's memory model is respected and optimizations will remain active. The [Solidity documentation](https://docs.soliditylang.org/en/latest/assembly.html#memory-safety) describes that doing so requires the assembly block to only access memory ranges that (1) were already allocated by Solidity, those (2) allocated manually using the free memory pointer, as well as (3) the scratch space.

| First Offset | Last Offset | Size     | Description  |
|--------------|-------------|----------|--------------|
| `0x00`       | `0x3f`      | 64 bytes | **Scratch Space**, temporary memory used for hashing opcodes. |
| `0x40`       | `0x5f`      | 32 bytes | **Free Memory Pointer**, points to the first unallocated byte in memory. Also represents the currently allocated memory size. |
| `0x60`       | `0x7f`      | 32 bytes | The **Zero Slot**, should always be the zero-value. |

The documentation's _Advanced Safe Use of Memory_ section describes circumstances under which using more than 64 bytes of scratch space from offset `0x00` until `0x7f` is permissible: Either when by the end of the assembly block both the free memory pointer and the zero slot are restored. Or when the assembly block terminates and execution could never return to high-level Solidity code in the first place.

**A**: The `memSafe0` function overwrites memory starting at offset `0x00`, until `0x63`, with the parameter values passed to the function. Finally, it restores the free memory pointer to its original value. However, it does not reset the Zero Slot, of which some bytes have been overwritten, back to the zero value. Therefore, the inline assembly block does not respect Solidity's memory model and cannot be annotated as "memory-safe".

**B**: The `memSafe1` function overwrites memory starting at offset `0x00`, until `0x43`, with the parameter values passed to the function. This means the left-most 4 bytes of the Free Memory Pointer are overwritten. At the end of the function we see that the memory range from `0x24` to `0x43` is overwritten with the zero-value. Which, in turn, means that the bytes of the Free Memory Pointer, that had been overwritten, are reset to zero. Due to [memory expansion costs](https://www.evm.codes/about#memoryexpansion), it's safe to assume that these bytes were zero to begin with.

**C**: The `memSafe2` function only, and exactly, overwrites the Scratch Space, then makes use of the [keccak256 opcode,](https://www.evm.codes/?fork=cancun#20) which hashes the Scratch Space and puts the resulting hash on top of the stack. This perfectly stays within the Solidity memory model without any tricks.

**D**: The `memSafe3` function does not respect Solidity's memory layout, but in this case it's permissible as it always reverts and therefore never returns execution back to Solidity within the current EVM call context.

</HiddenSolution>

---

### Question 2 of 8

```plaintext showLineNumbers copy filename="WhichFunc.sol"
pragma solidity ^0.8.0;

contract WhichFunc {
    function whichFunc(bytes4 selector, address a, uint256 b) external pure {
        assembly {
            let m := mload(0x40)

            mstore(add(m, 0x18), a)
            mstore(add(m, 0x04), shr(224, selector))
            mstore(add(m, 0x38), b)
            mstore(m, 0x38)

            mstore(0x40, add(m, add(0x38, 0x20)))
        }
    }
}
```

Which high level built-in function has the same resulting memory as the logic in `WhichFunc.whichFunc` when provided with the same parameters?

<ul>
  <li><input type="checkbox" /> A. `abi.encode` </li>
  <li><input type="checkbox" /> B. `abi.encodePacked` </li>
  <li><input type="checkbox" /> C. `abi.encodeWithSelector` </li>
  <li><input type="checkbox" /> D. `abi.decode` </li>
</ul>

<HiddenSolution>
**Correct is B.**

The given inline assembly block puts the values of the parameters passed to the function into memory starting from the offset returned by loading the current Free Memory Pointer's value at `0x40`.

* `let m := mload(0x40)`: Loads the Free Memory Pointer value to allocate memory for storing the passed parameter values in memory.
* `mstore(add(m, 0x18), a)`: Parameter `a` is written into memory from `m + 0x18` until `m + 0x37`, but as it is of address type, only the right-most 20 bytes will contain the expected address. Therefore the address will be located from `m + 0x24` until `m + 0x37`.
* `mstore(add(m, 0x04), shr(224, selector))`: The selector parameter, consisting only of 4 bytes aligned to the left-most side, is right-shifted by 224 bits (28 bytes) moving the value all the way to the right. This is then written from `m + 0x04` until `m + 0x23` – although, the actual value will only occupy `m + 0x20` until `m + 0x23`.
* `mstore(add(m, 0x38), b)`: Parameter `b` is written to occupy the full 32 bytes slot from `m + 0x38` to `m + 0x57` as it is a `uint256` type, the value of which may use the entire slot.
* `mstore(m, 0x38)`: Then, the static value `0x38` (56 in decimal) is written into the full 32 bytes starting at `m`, until `m + 0x1f`. 4 bytes + 20 bytes + 32 bytes = 56 bytes – therefore, this is the length of all parameters combined without padding, ie. packed.
* `mstore(0x40, add(m, add(0x38, 0x20)))`: Finally, the allocation of memory is completed by adding the length of the packed parameter values, as well as the size of the slot storing that length, to the Free Memory Pointer.

Based on this, it appears that this function is most similar to [`abi.encodePacked`](https://docs.soliditylang.org/en/latest/abi-spec.html#non-standard-packed-mode). You might have assumed that it was [`encodeWithSelector`](https://docs.soliditylang.org/en/latest/units-and-global-variables.html#abi-encoding-and-decoding-functions) instead due to the mention of a `selector`, but this function returns the values ABI-encoded, where they stay padded to each take a full 32 bytes-slot in memory.

You can validate the result in [chisel](https://getfoundry.sh/chisel/overview/) and check the resulting memory with `!memdump`.
</HiddenSolution>

---

### Question 3 of 8

Which of the following has the greatest absolute value?

<ul>
  <li><input type="checkbox" /> A. `type(int160).min` </li>
  <li><input type="checkbox" /> B. `type(int160).max` </li>
  <li><input type="checkbox" /> C. `type(int256).min` </li>
  <li><input type="checkbox" /> D. `type(int256).max` </li>
</ul>

<HiddenSolution>
**Correct is C.**

</HiddenSolution>

---

### Question 4 of 8

```
0xffffffffffffffffffffffffffffffffffffffffffffffffffffffffffffff00
```

Which int256 value corresponds to this underlying low-level decimal value?

<ul>
  <li><input type="checkbox" /> A. $`115792089237316195423570985008687907853269984665640564039457584007913129639680`$ </li>
  <li><input type="checkbox" /> B. $`-452312848583266388373324160190187140051835877600158453279131187530910662400`$ </li>
  <li><input type="checkbox" /> C. $`256`$ </li>
  <li><input type="checkbox" /> D. $`-256`$ </li>
</ul>

<HiddenSolution>
**Correct is D.**


</HiddenSolution>

---

### Question 5 of 8

```plaintext showLineNumbers copy filename="WhichRange.sol"
pragma solidity ^0.8.0;

contract WhichRange {
    function whichRange(uint32 selector, bytes16 a) external pure {
        assembly {
            mstore(0x00, selector)
            mstore(0x20, a)
        }
    }
}
```

Which byte range in memory contains the values in their packed representation in `WhichRange.whichRange` (excluding unused bytes)?

<ul>
  <li><input type="checkbox" /> A. $`["0x00", "0x40"]`$ </li>
  <li><input type="checkbox" /> B. $`["0x1c", "0x30"]`$ </li>
  <li><input type="checkbox" /> C. $`["0x00", "0x40")`$ </li>
  <li><input type="checkbox" /> D. $`["0x1c", "0x30")`$ </li>
</ul>

<HiddenSolution>
**Correct is D.**

</HiddenSolution>

---

### Question 6 of 8

```plaintext showLineNumbers copy filename="KeccakInput.sol"
pragma solidity ^0.8.0;

contract KeccakInput {
    function keccakInput() external pure {
        bytes4 selector = 0xefefefef;
        address addr = 0x3111327EdD38890C3fe564afd96b4C73e8101747;
        uint256 num = 123;

        assembly {
            let m := mload(0x40)

            mstore(0x00, selector)
            mstore(0x20, addr)
            mstore(0x40, num)

            let hash := keccak256(0x1c, 0x44)

            mstore(0x40, m)
        }
    }
}
```

What is the input of the keccak256 in `KeccakInput.keccakInput`?

<ul>
  <li><input type="checkbox" /> A. `0xefefefef0000000000000000000000003111327edd38890c3fe564afd96b4c73e81017470000000000000000000000000000000000000000000000000000000000000123` </li>
  <li><input type="checkbox" /> B. `0x000000000000000000000000000000003111327edd38890c3fe564afd96b4c73e81017470000000000000000000000000000000000000000000000000000000000000123` </li>
  <li><input type="checkbox" /> C. `0xefefefef0000000000000000000000003111327edd38890c3fe564afd96b4c73e8101747000000000000000000000000000000000000000000000000000000000000007b` </li>
  <li><input type="checkbox" /> D. `0x000000000000000000000000000000003111327edd38890c3fe564afd96b4c73e8101747000000000000000000000000000000000000000000000000000000000000007b` </li>
</ul>

<HiddenSolution>
**Correct is D.**


</HiddenSolution>

---

### Question 7 of 8

```plaintext showLineNumbers copy filename="FuncSel.sol"
pragma solidity ^0.8.0;

contract FuncSel {
    function funcSel(
        string memory str, 
        uint256[] memory b, 
        address addr, 
        uint256 num
    ) external pure {}
}
```

What is the function selector for `FuncSel.funcSel`

<ul>
  <li><input type="checkbox" /> A. `“funcSel(string,uint256[],address,uint256)”` </li>
  <li><input type="checkbox" /> B. `“funcSel(string, uint256[], address, uint256)”` </li>
  <li><input type="checkbox" /> C. `0xe833c93d` </li>
  <li><input type="checkbox" /> D. `0x37ce562a` </li>
</ul>

<HiddenSolution>
**Correct is C.**


</HiddenSolution>

---

### Question 8 of 8

```plaintext showLineNumbers copy filename="WhichCalldata.sol"
pragma solidity ^0.8.0;

contract WhichCalldata {
    function a(
        address[3] memory addrs, 
        uint256 val
    ) external pure {}

    function b(
        address addr0,
        address addr1,
        address addr2,
        uint256 val
    ) external pure {}
}
```

Given the following calldata (initial 4 bytes hidden) used to call the `WhichCalldata` contract, which function and parameters are being provided?

`0xffffffff0000000000000000000000003111327edd38890c3fe564afd96b4c73e8101747000000000000000000000000c02aaa39b223fe8d0a0e5c4f27ead9083c756cc2000000000000000000000000d8da6bf26964af9d7eed9e03e53415d37aa960450000000000000000000000000000000000000000000000000000000000000123`

<ul>
  <li><input type="checkbox" /> A. `b(0x3111327EdD38890C3fe564afd96b4C73e8101747, 0xC02aaA39b223FE8D0A0e5C4F27eAD9083C756Cc2, 0xd8dA6BF26964aF9D7eEd9e03E53415D37aA96045, 0x123)` </li>
  <li><input type="checkbox" /> B. `a([0x3111327EdD38890C3fe564afd96b4C73e8101747, 0xC02aaA39b223FE8D0A0e5C4F27eAD9083C756Cc2, 0xd8dA6BF26964aF9D7eEd9e03E53415D37aA96045], 0x123)` </li>
  <li><input type="checkbox" /> C. `b(0x3111327EdD38890C3fe564afd96b4C73e8101747, 0xC02aaA39b223FE8D0A0e5C4F27eAD9083C756Cc2, 0xd8dA6BF26964aF9D7eEd9e03E53415D37aA96045, 123)` </li>
  <li><input type="checkbox" /> D. Cannot determine </li>
</ul>

<HiddenSolution>
**Correct is D.**

</HiddenSolution>

---

import { Card, Cards } from 'nextra/components'
import { NewsletterIcon } from '@components/icons'

<Cards>
  <Card
    icon={<NewsletterIcon />}
    title="← RACE #41 Of The Secureum Bootcamp Epoch∞"
    href="/posts/2025/6/19/race-41-of-the-secureum-bootcamp-epoch-infinity/"
  />
</Cards>
